; ACME 0.96.4
;
; UNREEEAL SUPERHERO 3 TRIBUTE 
; GROUiK / FRENCH TOUCH
; 01/2020
; 
; subroutines part: display volumeter / scrollings / (ugly) K7 animation
;
!convtab "a2txt.bin" ; 
!sl "lbl_sub.txt"		

!source "lbl_pts.txt"

		*= $6000 		; ORG = $6000

; PAGE ZERO

cDisplayFreq		= $A0
ADR					= $A1   ; +$A2
iText               = $A3   ; +$A4
CountScroll         = $A5
nextchar            = $A6
cSpace              = $A7
sScroll             = $A8
iText2              = $A9   ; +$AA
iScroll2            = $AB	
; ==============================================================================
DIS_FREQ
!zone
		LDA cDisplayFreq
		BEQ .go
		DEC cDisplayFreq
		RTS

.go
        LDA #6
        STA cDisplayFreq
; display
; freq A		
    	LDA V1AYREGS+08             ; chan A/AY1
		LSR
        AND #%00001111
        TAX
        LDA TFreqL,X
		STA .OFFa+1
		LDA TFreqH,X
		STA .OFFa+2

        LDY #10
		LDX #02
.b2a	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFa	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2a
		
; freq B
		LDA V2AYREGS+08             ; chan A/AY2
        LSR
        AND #%00001111
        TAX
		LDA TFreqL,X
		STA .OFFb+1
		LDA TFreqH,X
		STA .OFFb+2

        LDY #12
		LDX #02
.b2b	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFb	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2b

; freqC
    	LDA V1AYREGS+09             ; chan B/AY1
        LSR
        AND #%00001111
        TAX
		LDA TFreqL,X
		STA .OFFc+1
		LDA TFreqH,X
		STA .OFFc+2

        LDY #14
		LDX #02
.b2c	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFc	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2c

; freq D	
    	LDA V2AYREGS+09             ; chan B/AY2
        LSR
        AND #%00001111
        TAX
		LDA TFreqL,X
		STA .OFFd+1
		LDA TFreqH,X
		STA .OFFd+2

        LDY #16
		LDX #02
.b2d	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFd	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2d
		
; freq E
		LDA V1AYREGS+10             ; chan C/AY1
        LSR
        AND #%00001111
        TAX
		LDA TFreqL,X
		STA .OFFe+1
		LDA TFreqH,X
		STA .OFFe+2

        LDY #18
		LDX #02
.b2e	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFe	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2e

; freqF
    	LDA V2AYREGS+10             ; chan C/AY2
        LSR
        AND #%00000111
        TAX
		LDA TFreqL,X
		STA .OFFf+1
		LDA TFreqH,X
		STA .OFFf+2

        LDY #20
		LDX #02
.b2f	LDA TTB,X
		STA ADR
		LDA TTH,X
		STA ADR+1
	
.OFFf	LDA TFreq1,X
		STA (ADR),Y
		DEX
		BPL .b2f
				
		RTS
; =============================================================================
SCROLLTEXT
BASELINE = $750
!zone
        LDA CountScroll
        BNE +
        LDA sScroll
        BEQ SCROLL_RL
        JMP SCROLL_LR
+       DEC CountScroll
        RTS
; -------------------------------------
SCROLL_RL
        ; scroll right/left
        LDX #01
-    	INX
        LDA BASELINE,X		
		DEX				
		STA BASELINE,X		
		INX				
		CPX #38			
		BNE -
        LDA nextchar
        STA BASELINE+38	
        BNE .next       ; always

SCROLL_LR
        ; scroll left/right
        LDX #38
-    	DEX
        LDA BASELINE,X		
		INX				
		STA BASELINE,X		
		DEX
        CPX #01							
		BNE -
        LDA nextchar
        STA BASELINE+01
        ; ---

        ; check marker/inject new letter
        ; part II : entrée d'un nouveau caractère issu de la chaine à afficher
.next
		LDA cSpace
        BEQ +
        DEC cSpace
        RTS
+       INC iText
		BNE +
		INC iText+1       
+       LDY #00
		LDA (iText),Y	
		BEQ .stopscroll         ; add tempo
        CMP #$03                ; add a empty (space) line 
        BEQ .emptyline
        CMP #$02                ; change scroll sens
        BEQ .changesens
        CMP #$01                ; end of text
        BNE +
        ; reinit
        LDA #<(TextScroll-1)
        STA iText
        LDA #>(TextScroll-1)
        STA iText+1
        LDA #00
        STA cSpace
        STA sScroll
        RTS
        ; ---
+       LDY sScroll
        BNE +              		
		STA nextchar        ; if R->L 
        RTS
+       ; if L->R
        LDY iScroll2 
        LDA (iText2),Y
        STA nextchar
        DEC iScroll2
        RTS

.stopscroll
        LDA #255
        STA CountScroll
        JMP .next
.emptyline
        LDA #$A0        ; space
        STA nextchar
        LDA #38
        STA cSpace
        JMP .next
.changesens
        LDA sScroll
        EOR #$01
        STA sScroll
        BEQ +
        LDA #38
        STA iScroll2
        LDA iText        
        STA iText2
        LDA iText+1
        STA iText2+1
+       JMP .next
; =============================================================================
ANIM_1P1
!zone
        LDA #$18 : STA $30E2
        LDA #$46 : STA $30E3
        LDA #$0C : STA $34E2
        LDA #$4C : STA $34E3
        LDA #$0C : STA $38E2
        LDA #$4C : STA $38E3
        LDA #$0C : STA $3CE2
        LDA #$4C : STA $3CE3
        LDA #$18 : STA $2162
        LDA #$46 : STA $2163

        LDA #$31 : STA $30E5
        LDA #$0E : STA $30E6
        LDA #$19 : STA $34E5
        LDA #$18 : STA $34E6
        LDA #$19 : STA $38E5
        LDA #$18 : STA $38E6
        LDA #$19 : STA $3CE5
        LDA #$18 : STA $3CE6
        LDA #$71 : STA $2165
        LDA #$0C : STA $2166

        RTS
; =============================================================================
ANIM_1P2
!zone
        LDA #$18 : STA $50E2
        LDA #$46 : STA $50E3
        LDA #$0C : STA $54E2
        LDA #$4C : STA $54E3
        LDA #$0C : STA $58E2
        LDA #$4C : STA $58E3
        LDA #$0C : STA $5CE2
        LDA #$4C : STA $5CE3
        LDA #$18 : STA $4162
        LDA #$46 : STA $4163

        LDA #$31 : STA $50E5
        LDA #$0E : STA $50E6
        LDA #$19 : STA $54E5
        LDA #$18 : STA $54E6
        LDA #$19 : STA $58E5
        LDA #$18 : STA $58E6
        LDA #$19 : STA $5CE5
        LDA #$18 : STA $5CE6
        LDA #$71 : STA $4165
        LDA #$0C : STA $4166
        RTS
; =============================================================================
ANIM_2P1
!zone
        LDA #$18 : STA $30E2
        LDA #$47 : STA $30E3
        LDA #$0C : STA $34E2
        LDA #$4C : STA $34E3
        LDA #$0C : STA $38E2
        LDA #$4C : STA $38E3
        LDA #$0C : STA $3CE2
        LDA #$4C : STA $3CE3
        LDA #$38 : STA $2162
        LDA #$46 : STA $2163
        
        LDA #$31 : STA $30E5
        LDA #$0C : STA $30E6
        LDA #$19 : STA $34E5
        LDA #$18 : STA $34E6
        LDA #$19 : STA $38E5
        LDA #$18 : STA $38E6
        LDA #$19 : STA $3CE5
        LDA #$18 : STA $3CE6
        LDA #$31 : STA $2165
        LDA #$0C : STA $2166

        RTS
; =============================================================================
ANIM_2P2
!zone
        
        LDA #$18 : STA $50E2
        LDA #$47 : STA $50E3
        LDA #$0C : STA $54E2
        LDA #$4C : STA $54E3
        LDA #$0C : STA $58E2
        LDA #$4C : STA $58E3
        LDA #$0C : STA $5CE2
        LDA #$4C : STA $5CE3
        LDA #$38 : STA $4162
        LDA #$46 : STA $4163
        
        LDA #$31 : STA $50E5
        LDA #$0C : STA $50E6
        LDA #$19 : STA $54E5
        LDA #$18 : STA $54E6
        LDA #$19 : STA $58E5
        LDA #$18 : STA $58E6
        LDA #$19 : STA $5CE5
        LDA #$18 : STA $5CE6
        LDA #$31 : STA $4165
        LDA #$0C : STA $4166
        
        RTS
; =============================================================================
;                      00000000001111111111222222222233333333!!
;                      01234567890123456789012345678901234567!!
;                      01234567890                09876543210!!
TextScroll      !text " ",00,03
                !text "     UNREEEAL SUPERHERO 3 TRIBUTE     ",00,02 
                !text "  Featuring a PT3 TURBO SOUND PLAYER  ",00,02
                !text "for the Apple II with Mockingboard and",00,02
                !text " this stunning PT3 6 Channels version ",00,02
                !text "  of the famous track by KENET & REZ  ",00,03,00
                !text "          CODE & GFX: GROUiK          ",00,02
                !text "Original Z80 PT3 TS Player: S.V. BULBA",00,02
                !text "Original Music: KENET & REZ of course!",00,02
                !text ">> PT3 TS version:  I'M A TRACK MAN <<",00,02,03,00
                !text "              GREETZ TO:              ",00
                !text " 8-Bit-Shack * Brutal Deluxe * Cosine ",00,02
                !text " Fenarinarsa * g0blinish * NinjaForce ",00,02
                !text "       The AppleWin Team * VMW        ",00,02 
                !text "    and to all the folks keeping...   ",00,02
                !text " the APPLE II alive around the world! ",00,03,00
                !text "             FRENCH TOUCH             ",00,03,00
;                      0123456789012            2109876543210!!
                !byte $01       ; end of text
; $00: end of line / tempo | $01: end of text | $02: change sens | $03: empty line
; =============================================================================
; Colors1
; TFreq0  !byte $00,$00,$00
; TFreq1  !byte $00,$00,$40
; TFreq2  !byte $00,$00,$4C
; TFreq3  !byte $00,$D0,$4C
; TFreq4  !byte $00,$D9,$4C
; TFreq5  !byte $30,$D9,$4C
; TFreq6  !byte $31,$D9,$4C
; TFreq7  !byte $31,$D9,$4C

; Colors2
TFreq0  !byte $00,$00,$00
TFreq1	!byte $00,$00,$10
TFreq2  !byte $00,$00,$13
TFreq3  !byte $00,$90,$13
TFreq4	!byte $00,$9D,$13
TFreq5  !byte $C0,$9D,$13
TFreq6	!byte $C4,$9D,$13
TFreq7  !byte $C4,$9D,$13

TFreqA  !byte $00,$9D,$13

TFreqL	!byte <TFreq0,<TFreq1,<TFreq2,<TFreq3,<TFreq4,<TFreq5,<TFreq6,<TFreq7,<TFreqA
TFreqH	!byte >TFreq0,>TFreq1,>TFreq2,>TFreq3,>TFreq4,>TFreq5,>TFreq6,>TFreq7,>TFreqA

TTB		!byte $28+offx,$A8+offx,$28+offx
TTH		!byte $04,$04,$05

offx = 4
